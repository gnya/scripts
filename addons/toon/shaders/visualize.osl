/*
 * toon shader (0.4.2)
 * author: gnya
 */

#include "group_id.h"

#define SHADOW 0
#define TRANSPARENT 1
#define SHADOW_PROPS 2

/*
 * Linear congruential generator (m=100, a=19, c=3)
 * ref: https://en.wikipedia.org/wiki/Linear_congruential_generator
 */
float random(int seed) {
    return mod(19 * seed + 3, 100) / 100.0;
}

shader visualize(
    int Type = 0,
    
    output color Color = color(0.0)
) {
    if (Type == SHADOW || Type == SHADOW_PROPS) {
        int group, props;
        get_this_shadow_group(group, props);

        if (Type == SHADOW && group) {
            Color = color("hsv", random(group), 1.0, 1.0);
        } else if (Type == SHADOW_PROPS && props) {
            Color = color("hsv", random(props), 1.0, 1.0);
        }
    } else if (Type == TRANSPARENT) {
        int group;
        get_this_transparent_group(group);

        if (group) {
            Color = color("hsv", random(group), 1.0, 1.0);
        }
    }
}
